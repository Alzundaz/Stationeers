define DAYTIME 20
define CHARGECOLOR 2
define DISCHARGECOLOR 3
define CHARGESTATUS 0
define DISCHARGESTATUS 1

alias DaylightSensor d0
alias TimeClock d1
alias BatteryTimeLeft d2
alias BatteryTimeClock d3

alias time r0
alias batteryTimeLeft r1
alias batteryTimeClock r2

alias solarAngle r3
alias lastSolarAngle r4
alias batteryLevel r5
alias lastBatteryLevel r6
alias maxBatteryLevel r7
alias batteryStatus r8

alias var r9
alias secondVar r10

l lastSolarAngle DaylightSensor Vertical
lb lastBatteryLevel -400115994 Charge 1
lb var -1388288459 Charge 1
add lastBatteryLevel lastBatteryLevel var
move batteryTimeLeft 0

start:

jal doTimeClock
jal doBatteryTimeLeft
jal doBatteryTimeClock

yield
yield
j start

doTimeClock:
bdns DaylightSensor ra

l solarAngle DaylightSensor Vertical

brge solarAngle lastSolarAngle 2
sub solarAngle 360 solarAngle
move lastSolarAngle solarAngle
add solarAngle solarAngle 90
div time solarAngle 360
mul time time DAYTIME

move var time
push ra
jal convertTime
pop ra

bdns TimeClock ra
s TimeClock Setting var

j ra

doBatteryTimeLeft:

lb batteryLevel -400115994 Charge 1
lb var -1388288459 Charge 1
add batteryLevel batteryLevel var

lb maxBatteryLevel -400115994 Maximum 1
lb var -1388288459 Maximum 1
add maxBatteryLevel maxBatteryLevel var

sgt batteryStatus lastBatteryLevel batteryLevel
sub var batteryLevel lastBatteryLevel
abs var var
move lastBatteryLevel batteryLevel
breq var 0 4
breq batteryStatus DISCHARGESTATUS 2
sub batteryLevel maxBatteryLevel batteryLevel
div var batteryLevel var

floor var var
div batteryTimeLeft var 60
bdns BatteryTimeLeft ra

move var batteryTimeLeft
push ra
jal convertTime
pop ra

s BatteryTimeLeft Color CHARGECOLOR
breq batteryStatus CHARGESTATUS 2
s BatteryTimeLeft Color DISCHARGECOLOR
s BatteryTimeLeft Setting var

j ra

doBatteryTimeClock:
bdns DaylightSensor ra
bdns BatteryTimeClock ra

add batteryTimeClock time batteryTimeLeft
mod batteryTimeClock batteryTimeClock DAYTIME

move var batteryTimeClock
push ra
jal convertTime
pop ra

s BatteryTimeClock Color CHARGECOLOR
breq batteryStatus CHARGESTATUS 2
s BatteryTimeClock Color DISCHARGECOLOR
s BatteryTimeClock Setting var

j ra

convertTime:

floor secondVar var
sub var var secondVar
mul var var 60
floor var var
div var var 100
add var var secondVar

j ra
